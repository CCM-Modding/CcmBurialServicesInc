buildscript {
    repositories {
        maven { url = "http://maven.dries007.net/content/repositories/ccm/" }
        maven { url = "http://files.minecraftforge.net/maven/" }
        maven { url = "https://libraries.minecraft.net/" }
        mavenCentral()
    }
    
    dependencies {
		classpath 'com.github.abrarsyed.gmcp:GMCP:latest.integration'
    }
}

configurations {
    compile
    deployJars
}

repositories {
    maven { url = "http://maven.dries007.net/content/repositories/ccm/" }
    maven { url = "http://files.minecraftforge.net/maven/" }
    maven { url = "https://libraries.minecraft.net/" }
}

apply plugin: "gmcp"
apply plugin: "maven"

group = "ccm.burialservices"
version = "1.0.0"

targetCompatibility = 1.6
sourceCompatibility = 1.6

archivesBaseName = 'CcmBurialServicesInc'
minecraft {
	minecraftVersion = "1.6.4"
}

ext.BUILD_NUMBER = System.getenv().BUILD_NUMBER;
if (ext.BUILD_NUMBER == null) ext.BUILD_NUMBER = "0"
version = version + "." + ext.BUILD_NUMBER

dependencies {
    compile("ccm.placeableTools:PlaceableTools:" + project.minecraft.minecraftVersion + "-1.+:dev")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        expand 'version':project.version, 'mcversion':project.minecraft.minecraftVersion, 'placeableToolsVersion':configurations.compile.resolvedConfiguration.firstLevelModuleDependencies.findAll({ it.moduleName == "PlaceableTools"}).collect({ it.moduleVersion})[0].split("-")[1]
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'src'
    appendix = project.minecraft.minecraftVersion
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from (sourceSets.main.allSource.srcDirs) {
        include '**/*.java'
    }
    classifier = 'dev'
    appendix = project.minecraft.minecraftVersion
}

jar {
    appendix = project.minecraft.minecraftVersion
}

artifacts {
	archives jar
    archives sourcesJar
    archives deobfJar
}

uploadArchives {
    if (project.hasProperty("mavenUser") && project.hasProperty("mavenPassword")) {
        repositories {
            mavenDeployer {
                repository(url: "http://maven.dries007.net/content/repositories/ccm/") {
                    authentication(userName: mavenUser, password: mavenPassword)
                }
                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'Adds upgradable graves to MC.'
                        url 'https://github.com/CCM-Modding/CcmBurialServicesInc'

                        scm {
                            url 'https://github.com/CCM-Modding/CcmBurialServicesInc'
                            connection 'scm:git:git://github.com/CCM-Modding/CcmBurialServicesInc.git'
                            developerConnection 'scm:git:git@github.com:CCM-Modding/CcmBurialServicesInc.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/CCM-Modding/CcmBurialServicesInc/issues'
                        }

                        licenses  {
                            license  {
                                name 'MIT License'
                                url 'https://raw.github.com/CCM-Modding/CcmBurialServicesInc/master/LICENCE'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'Dries007'
                                name 'Dries007'
                                roles { role 'developer' }
                            }
                            developer {
                                id 'Claycorp'
                                name 'Claycorp'
                                roles { role 'architect' }
                            }
                        }
                    }
                }
            }
        }
    }
}